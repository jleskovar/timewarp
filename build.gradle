buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'maven'
    id 'signing'
}


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group               = "co.paralleluniverse"
version             = "0.1.0-SNAPSHOT"
status              = "integration"
description         = "Virtual Time for the JVM"
ext.url             = "http://puniverse.github.com/timewarp"
ext.vendor          = "Parallel Universe Software Co."
ext.licenseName     = "The MIT License"
ext.licenseUrl      = "http://opensource.org/licenses/MIT"
ext.scmUrl          = "https://github.com/puniverse/timewarp"
ext.scmConnection   = "https://github.com/puniverse/timewarp.git"

ext.distDir         = "$buildDir/dist"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.ow2.asm:asm:5.0.4'
    compile 'org.springframework:spring-webmvc:4.2.5.RELEASE'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'org.eclipse.jetty:jetty-server:9.3.8.v20160314'
    compile 'org.mortbay.jetty:jetty:6.1.26'
    testCompile 'junit:junit:4.10'
    testCompile 'com.ea.agentloader:ea-agent-loader:1.0.0'
}

jar {
    manifest {
        attributes(
	            "Built-By"                  :   System.getProperty("user.name"),
	            "Implementation-Title"      :   project.name,
	            "Implementation-Version"    :   version,
	            "Implementation-Vendor"     :   vendor,
	            "Premain-Class"             :   "co.paralleluniverse.vtime.JavaAgent",
                "Agent-Class"               :   "co.paralleluniverse.vtime.JavaAgent",
        )
    }
}

shadowJar {
    outputs.upToDateWhen { false }
        
    classifier = '' // 'shadow'
    destinationDir = file("$buildDir/libs")

    exclude 'META-INF/**'
    exclude 'licenses/**'
    exclude 'about.html'
    exclude 'javax/inject/**'
    exclude 'javax/servlet/**'
    exclude 'org/apache/log4j/lf5/**'
    exclude 'org/springframework/**'
    exclude 'org/aopalliance/**'
    exclude 'org/apache/commons/logging/**'
    exclude 'org/mortbay/**'
    exclude 'org/eclipse/jetty/**'
    exclude 'org/apache/log4j/xml/log4j.dtd'
    exclude '**/version.properties'
    exclude 'META-INF/INDEX.LIST'

    relocate 'org.objectweb.asm.', 'co.paralleluniverse.asm.'
            
    manifest.inheritFrom jar.manifest
}

javadoc {
    options.noDeprecated = true
    excludes = [ "**/Clock_.java","**/JavaAgent.java" ]
    options {
        links = [ "http://docs.oracle.com/javase/7/docs/api/" ]
    }
}

[build, install]*.dependsOn shadowJar
    
install.repositories.mavenInstaller {
    pom.whenConfigured {
        it.dependencies.clear()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
